
//-------------------------------------------------------------------------------------
CLASS ENGINEX_SCRIPTEVENTS_SERVERPACKETS NULL
//-------------------------------------------------------------------------------------

   VAR_START

		PUBLIC SORTEDLIST PIPELIST 0
      PUBLIC ARRAYLIST PIPEQUEUE 0

		PUBLIC SORTEDLIST BLOCKDATA 0

		PUBLIC SORTEDLIST READFILES 0
		PUBLIC SORTEDLIST WRITEFILES 0

   VAR_END

//-------------------------------------------------------------------------------------

   PUBLIC CONSTRUCT
      THIS.SET_FILE VOID 3 "3" "0" "#$Include/EngineX/ScriptEvents/ServerPackets/0_Die.l2f"
      THIS.SET_FILE VOID 3 "3" "8" "#$Include/EngineX/ScriptEvents/ServerPackets/8_ObjectDelete.l2f"
      THIS.SET_FILE VOID 3 "3" "12" "#$Include/EngineX/ScriptEvents/ServerPackets/12_NpcInfo.l2f"
      THIS.SET_FILE VOID 3 "3" "22" "#$Include/EngineX/ScriptEvents/ServerPackets/22_DropItem.l2f"
      THIS.SET_FILE VOID 3 "3" "25" "#$Include/EngineX/ScriptEvents/ServerPackets/25_NpcHtmlMessage.l2f"
      THIS.SET_FILE VOID 3 "3" "31" "#$Include/EngineX/ScriptEvents/ServerPackets/31_ActionFailed.l2f"
      THIS.SET_FILE VOID 3 "3" "33" "#$Include/EngineX/ScriptEvents/ServerPackets/33_InventoryUpdate.l2f"
      THIS.SET_FILE VOID 3 "3" "35" "#$Include/EngineX/ScriptEvents/ServerPackets/35_TargetSelected.l2f"
      THIS.SET_FILE VOID 3 "3" "36" "#$Include/EngineX/ScriptEvents/ServerPackets/36_TargetUnselected.l2f"
      THIS.SET_FILE VOID 3 "3" "37" "#$Include/EngineX/ScriptEvents/ServerPackets/37_AutoAttackStart.l2f"
      THIS.SET_FILE VOID 3 "3" "38" "#$Include/EngineX/ScriptEvents/ServerPackets/38_AutoAttackStop.l2f"
      THIS.SET_FILE VOID 3 "3" "41" "#$Include/EngineX/ScriptEvents/ServerPackets/41_ChangeWaitType.l2f"
      THIS.SET_FILE VOID 3 "3" "47" "#$Include/EngineX/ScriptEvents/ServerPackets/47_MoveToLocation.l2f"
      THIS.SET_FILE VOID 3 "3" "69" "#$Include/EngineX/ScriptEvents/ServerPackets/69_ShortCutInit.l2f"
      THIS.SET_FILE VOID 3 "3" "72" "#$Include/EngineX/ScriptEvents/ServerPackets/72_MagicSkillUse.l2f"
      THIS.SET_FILE VOID 3 "3" "74" "#$Include/EngineX/ScriptEvents/ServerPackets/74_CreatureSay.l2f"
      THIS.SET_FILE VOID 3 "3" "95" "#$Include/EngineX/ScriptEvents/ServerPackets/95_SkillList.l2f"
      THIS.SET_FILE VOID 3 "3" "98" "#$Include/EngineX/ScriptEvents/ServerPackets/98_SystemMessage.l2f"
      THIS.SET_FILE VOID 3 "3" "106" "#$Include/EngineX/ScriptEvents/ServerPackets/106_PledgeCrest.l2f"
      THIS.SET_FILE VOID 3 "3" "114" "#$Include/EngineX/ScriptEvents/ServerPackets/114_MoveToPawn.l2f"
      THIS.SET_FILE VOID 3 "3" "121" "#$Include/EngineX/ScriptEvents/ServerPackets/121_ValidateLocation.l2f"
      THIS.SET_FILE VOID 3 "3" "124" "#$Include/EngineX/ScriptEvents/ServerPackets/124_ChooseInventoryItem.l2f"
      THIS.SET_FILE VOID 3 "3" "135" "#$Include/EngineX/ScriptEvents/ServerPackets/135_EnchantResult.l2f"
      THIS.SET_FILE VOID 3 "3" "166" "#$Include/EngineX/ScriptEvents/ServerPackets/166_TutorialShowHtml.l2f"
      THIS.SET_FILE VOID 3 "3" "167" "#$Include/EngineX/ScriptEvents/ServerPackets/167_TutorialShowQuestionMark.l2f"
      THIS.SET_FILE VOID 3 "3" "169" "#$Include/EngineX/ScriptEvents/ServerPackets/169_TutorialCloseHtml.l2f"
      THIS.SET_FILE VOID 3 "3" "185" "#$Include/EngineX/ScriptEvents/ServerPackets/185_MyTargetSelected.l2f"
      THIS.SET_FILE VOID 3 "3" "208" "#$Include/EngineX/ScriptEvents/ServerPackets/208_MultiSellList.l2f"
      THIS.SET_FILE VOID 3 "3" "241" "#$Include/EngineX/ScriptEvents/ServerPackets/241_RadarControl.l2f"
      THREAD PIPELOOP
      SLEEP 1
   RETURN VOID

//-------------------------------------------------------------------------------------

   PUBLIC SET_FILE 3 MODE PID FILEPATH
      THIS.READFILES.REMOVE "<&PID&>"
      THIS.WRITEFILES.REMOVE "<&PID&>"
      IF ( ( MODE == 1 ) || ( MODE == 3 ) )
         THIS.READFILES.ADD "#$<&FILEPATH&>" "<&PID&>"
      ENDIF
      IF ( ( MODE == 2 ) || ( MODE == 3 ) )
         THIS.WRITEFILES.ADD "#$<&FILEPATH&>" "<&PID&>"
      ENDIF
      SLEEP 1
   RETURN VOID

//-------------------------------------------------------------------------------------

   PUBLIC ADD_PIPE 3 PID FUNCTION FILE

      IF ( "THIS.PIPELIST.CONTAINS_KEY <&PID&>" == FALSE )
         DEFINE ARRAYLIST PIPECALLS 0
         THIS.PIPELIST.ADD PIPECALLS "<&PID&>"
         DELETE PIPECALLS
         SET_EVENT "<&SCRIPTEVENT_SERVERPACKET&>" "<&SYSTEM_CURRENTFILE&>" RECEIVE "<&PID&>"
      ENDIF

      DEFINE SORTEDLIST PIPECALL 0
      PIPECALL.ADD "#$<&FUNCTION&>" "FUNCTION"
      IF ( FILE.CLASSNAME != "#$NULL" )
         PIPECALL.ADD "#$<&FILE&>" "FILE"
      ENDIF
      DEFINE STRING PID2STR "<&PID&>"
      THIS.PIPELIST.PID2STR.ADD "PIPECALL"

      SLEEP 1
	RETURN VOID

//-------------------------------------------------------------------------------------

   PUBLIC ADD_BLOCK 3 PID FUNCTION FILE

      IF ( "THIS.BLOCKDATA.CONTAINS_KEY <&PID&>" == FALSE )
         DEFINE ARRAYLIST BLOCKCALLS 0
         THIS.BLOCKDATA.ADD BLOCKCALLS "<&PID&>"
         DELETE BLOCKCALLS
         SET_EVENT "<&SCRIPTEVENT_SERVERPACKET&>" "<&SYSTEM_CURRENTFILE&>" RECEIVE "<&PID&>"
         BLOCK "<&PID&>"
      ENDIF

      DEFINE SORTEDLIST BLOCKCALL 0
      BLOCKCALL.ADD "#$<&FUNCTION&>" "FUNCTION"
      IF ( FILE.CLASSNAME != "#$NULL" )
         BLOCKCALL.ADD "#$<&FILE&>" "FILE"
      ENDIF
      DEFINE STRING PID2STR "<&PID&>"
      THIS.BLOCKDATA.PID2STR.ADD "BLOCKCALL"

      SLEEP 1
	RETURN VOID

//-------------------------------------------------------------------------------------

	PUBLIC RECEIVE 1 PACKET
      DEFINE NULL THIS 0
      THIS = ENGINEX.SCRIPTEVENTS.SERVERPACKETS

		DEFINE INT PID 0
		PACKET.READ_BYTE PID
		PACKET.RESET_INDEX

      DEFINE STRING PID2STR "<&PID&>"

      IF ( "THIS.PIPELIST.CONTAINS_KEY <&PID&>" == TRUE )
         FOR PIPE_I 0 "<&THIS.PIPELIST.PID2STR.COUNT&>" 1
            DEFINE ARRAYLIST PIPEQUEUED 0
            PIPEQUEUED.ADD "#$<&PID2STR&>"
            PIPEQUEUED.ADD "#i<&PIPE_I&>"
            PIPEQUEUED.ADD PACKET
            THIS.PIPEQUEUE.ADD PIPEQUEUED
            DELETE PIPEQUEUED
         NEXT
      ENDIF

      DEFINE INT VALID 1
      IF ( "THIS.BLOCKDATA.CONTAINS_KEY <&PID&>" == TRUE )
         FOR BLOCK_I 0 "<&THIS.BLOCKDATA.PID2STR.COUNT&>" 1
            IF ( "THIS.BLOCKDATA.PID2STR.BLOCK_I.CONTAINS_KEY FILE" == TRUE )
               CALL_EXTERN "<&THIS.BLOCKDATA.PID2STR.BLOCK_I.#$FILE&>" "<&THIS.BLOCKDATA.PID2STR.BLOCK_I.#$FUNCTION&>" VALID 1 PACKET
            ELSE
               CALL "<&THIS.BLOCKDATA.PID2STR.BLOCK_I.#$FUNCTION&>" VALID 1 PACKET
            ENDIF
            PACKET.RESET_INDEX
            IF ( VALID == FALSE )
               BREAK 3
            ENDIF
         NEXT
      ENDIF
      IF ( VALID == TRUE )
         INJECTBB_CLIENT PACKET
      ENDIF

	RETURN VOID

//-------------------------------------------------------------------------------------

	PUBLIC PIPELOOP 0
      DEFINE NULL THIS 0
      THIS = ENGINEX.SCRIPTEVENTS.SERVERPACKETS
      WHILE ( THIS.CLASSNAME == #$STRING )
         SLEEP 1
      WEND
      WHILE ( TRUE == TRUE )
         IF ( THIS.PIPEQUEUE.COUNT != ZERO )
            THREAD PIPE
            WHILE ( THIS.PIPEQUEUE.0.COUNT != ZERO )
               SLEEP 1
            WEND
            THIS.PIPEQUEUE.REMOVE 0
         ENDIF
         SLEEP 1
      WEND
   RETURN VOID

//-------------------------------------------------------------------------------------

	PUBLIC PIPE 0

      DEFINE STRING PID2STR "<&THIS.PIPEQUEUE.0.0&>"
      DEFINE INT CALLDATA_I "<&THIS.PIPEQUEUE.0.1&>"
      DEFINE BYTEBUFFER PACKET 0
      PACKET = THIS.PIPEQUEUE.0.2.CLONE
      THIS.PIPEQUEUE.0.CLEAR

      IF ( "THIS.PIPELIST.PID2STR.CALLDATA_I.CONTAINS_KEY FILE" == TRUE )
         CALL_EXTERN "<&THIS.PIPELIST.PID2STR.CALLDATA_I.#$FILE&>" "<&THIS.PIPELIST.PID2STR.CALLDATA_I.#$FUNCTION&>" VOID 1 PACKET
      ELSE
         CALL "<&THIS.PIPELIST.PID2STR.CALLDATA_I.#$FUNCTION&>" VOID 1 PACKET
      ENDIF

   RETURN VOID

//-------------------------------------------------------------------------------------

   PUBLIC READ 1 PACKET

		DEFINE INT PID 0
		PACKET.RESET_INDEX
		PACKET.READ_BYTE PID
		PACKET.RESET_INDEX

      DEFINE SORTEDLIST DATA 0
      IF ( "THIS.READFILES.CONTAINS_KEY <&PID&>" == TRUE )
         DEFINE STRING PID2STR "<&PID&>"
         CALL_EXTERN "<&THIS.READFILES.PID2STR&>" READ DATA 1 PACKET
      ENDIF

      SLEEP 1
   RETURN DATA

//-------------------------------------------------------------------------------------

   PUBLIC WRITE 3 PID DATA AUTOINJECT
      DEFINE BYTEBUFFER PACKET 0
      IF ( THIS.WRITEFILES.CONTAINS_KEY "<&PID&>" == TRUE )
         DEFINE STRING PID2STR "<&PID&>"
         CALL_EXTERN "<&THIS.WRITEFILES.PID2STR&>" WRITE PACKET 1 DATA
      ENDIF
      IF ( AUTOINJECT.TYPE == INT )
         IF ( AUTOINJECT == TRUE )
            INJECTBB_CLIENT PACKET
         ENDIF
      ENDIF
      SLEEP 1
   RETURN PACKET

//-------------------------------------------------------------------------------------
END_CLASS
//-------------------------------------------------------------------------------------
