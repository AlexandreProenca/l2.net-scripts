//LEVEL AND MANOR SCRIPT V0.1 by DarkWrath
//Original Script : Levelscript v0.2a (c) by Asmodin88 , slothmo, CHaNG, and mpj123
//Also thanks to Oddi and Spider for Suggestions / Bug Reports / Comments or whatever =P
//Stuck test and bug-fixes by Oddi
//TO DO :1. Companion script on buffer that sends a UDP packet when under attack , making the fighter change target and assist
//     2. 

////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////SETTINGS///////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////

//debug mode? (0 = off, 1 = on)
DEFINE INT DEBUG 0
//targeting mode. see script commands thread for further information
SET_TARGETING COMBAT TWO
SET_TARGETING INBOX #i0
SET_TARGETING ALIVE ZERO

////////GENERAL SETTINGS//////////
//what type of char is this? melee = 1, archer = 2, mage = 3
DEFINE INT CHARTYPE 1
//is this char a spoiler?
DEFINE INT ISSPOILER 0
//Are going to use manor?
DEFINE INT MANOR 0
//set here if this char is the "Train Leader" (does give the target to kill)
DEFINE INT ISLEADER 1
//set this 1 to move to the mob before attacking it. try this if you get stuck alot
DEFINE INT MOVETO_TARGET 1
//set this to 1 to enable stuck-check
DEFINE INT STUCK_CHECK 1
//distance to move to target before starting to attack? (dont set too low if you are mage or archer)
DEFINE INT MOVETO_DISTANCE 90
//Type of soulshot : NG=1 , D=2 and so on. 0 means disable
DEFINE INT SSID 0
//TYPE of spiritshot: NG=1 , D=2 and so on. 0 means disable
DEFINE INT SPSID 0
//Do you want to set a border box? 0 Disable , 1 Enable
DEFINE INT BRDBOX 0
//Set the size of the border box , first number is the radius on x axis , second on y
DEFINE INT XRADIUS 1100
DEFINE INT YRADIUS 1100

////////REST SETTINGS//////////
//set here at what hp/mp to rest. (in %)
DEFINE INT RESTHPPERCENT 1
DEFINE INT RESTMPPERCENT 1
//set here until what hp/mp to rest (in %)
DEFINE INT UNTILHPPERCENT 100
DEFINE INT UNTILMPPERCENT 100

////////LOGOUT SETTINGS//////////
//time to wait when dead before logout, set 0 to disable
DEFINE INT LOGOUTTIME 0
//Max time of Deaths before logout, set 0 to disable
DEFINE INT MAXDEATHS 0

////////ATTACK SETTINGS//////////
//under what hp of mob should we use normal attack? (mage only, melee and archer will always use attack)
DEFINE INT ATTACKHP 1
//delay between attack commands 
//(recommended to set rather high. randomly generated between MIN and MAX defined everytime used)
DEFINE INT MIN_ATTACKDELAY 1000
DEFINE INT MAX_ATTACKDELAY 1100
DEFINE INT SOULSHOTID 0
////////SPOILER SETTINGS//////////
//define skill id of spoil skill here (only used when isspoiler = 1)
DEFINE INT SPOILSKILLID 254
//delay after spoil skill is used.
DEFINE INT MIN_SPOILDELAY 200
DEFINE INT MAX_SPOILDELAY 500
//define skill id of sweep skill here (only used when isspoiler = 1)
DEFINE INT SWEEPSKILLID 42
//delay after sweep skill is used. (randomly generated between MIN and MAX defined everytime used)
DEFINE INT MIN_SWEEPDELAY 150
DEFINE INT MAX_SWEEPDELAY 300
//Skill Id to use while the character is at a distance to the mob , 0=disable
DEFINE INT RANGEDID 0
//Defines the distance at which to use the ranged skill
DEFINE INT SKLRANGE 0

//////MANOR SETTINGS/////
///define the id of the seed
DEFINE INT SEEDID 0
///define the id of the harvester
DEFINE INT HARVID 0

////////ATTACK SKILL//////////
//any skill to use to attack? (set 0 to disable, MUST be set as mage!)
DEFINE INT ATTACKSKILLID 0
DEFINE INT ATTACKSKILLID2 0
//above what mp should we use a skill to attack? (melee/archer only, mage always use skill) (in %)
DEFINE INT SKILLMPPERCENT 55
//delay after skill is used (randomly generated between MIN and MAX defined everytime used)
DEFINE INT MIN_SKILLDELAY 400
DEFINE INT MAX_SKILLDELAY 700

////////LOW HP SKILL//////////
//any skill to use when low hp? (skill id) set 0 to disable
//IMPORTANT: use this only for drains etc, since its used on the MOB. DONT put heals here!
DEFINE INT LOWHPSKILLID 0
//under what hp should lowhp skill be used on mob? (in %)
DEFINE INT LOWHPPERCENT 3
//delay after lowhp skill is used (randomly generated between MIN and MAX defined everytime used)
DEFINE INT MIN_LOWHPDELAY 400
DEFINE INT MAX_LOWHPDELAY 600

////////LOW MP SKILL//////////
//any skill to use when low mp? (this is for body to mind / pain of saggitarius / invocation etc) set 0 to disable
//hint: unlike lowhp skill, this one is used when NOT in combat with mob
DEFINE INT LOWMPSKILLID 0
//under what mp should we use it? (in %)
DEFINE INT LOWMPPERCENT 66
//delay after lowmp skill is used (randomly generated between MIN and MAX defined everytime used)
DEFINE INT MIN_LOWMPDELAY 1000
DEFINE INT MAX_LOWMPDELAY 2000

////////PICKUP SETTINGS//////////
//should this char pickup dropped items?
DEFINE INT PICKUP 1
//pick items first? set 0 to only pick items when not under attack (recommended)
DEFINE INT PICKFIRST 0
//set a range to pick up loot (recommended: ~200 for Melee's, for Mages and Archers higher, needs testing)
DEFINE INT LOOT_RANGE 200
//delay between pickup commands
DEFINE INT MIN_PICKUPDELAY 150
DEFINE INT MAX_PICKUPDELAY 300

////////ETC SETTINGS//////////
//delays after targeting and pickup (randomly generated between MIN and MAX defined everytime used)
DEFINE INT MIN_TARGETDELAY 1000
DEFINE INT MAX_TARGETDELAY 1500

//****************************Timevars*****************************//
DEFINE INT StartTime 0
DEFINE INT EndTime 0
DEFINE INT UsedTime 0
DEFINE INT MaxTime 200000000
DEFINE INT MaxPickTime 50000000 //thats 5 seconds
//****************************************************************//

////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////////ONLY EDIT FROM HERE ON IF YOU KNOW WHAT YOU ARE DOING!//////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////




//internal used variables, careful with editing them
DEFINE INT ONE 1
DEFINE INT ZERO 0
DEFINE INT TEMP 0
DEFINE INT HUNDRED 100
DEFINE INT THREE 3
DEFINE INT COUNT 0
DEFINE INT DEATHCOUNT 0
DEFINE INT Rest 0
DEFINE INT RESTHP 0
DEFINE INT RESTMP 0
DEFINE INT UNTILHP 0
DEFINE INT UNTILMP 0
DEFINE INT LOWHP 0
DEFINE INT LOWMP 0
DEFINE INT SKILLMP 0
DEFINE INT DISTANCE_TO_TARGET 0
DEFINE INT PICKUP_X 0
DEFINE INT PICKUP_Y 0
DEFINE DOUBLE X 0
DEFINE DOUBLE Y 0
DEFINE DOUBLE Range 100
DEFINE DOUBLE BORDERX 0
DEFINE DOUBLE BORDERY 0

//calc some stuff
LOWHP = CHAR_MAX_HP * LOWHPPERCENT
LOWHP = LOWHP / HUNDRED
LOWMP = CHAR_MAX_MP * LOWMPPERCENT
LOWMP = LOWMP / HUNDRED
SKILLMP = CHAR_MAX_MP * SKILLMPPERCENT
SKILLMP = SKILLMP / HUNDRED
RESTHP = CHAR_MAX_HP * RESTHPPERCENT
RESTHP = RESTHP / HUNDRED
RESTMP = CHAR_MAX_MP * RESTMPPERCENT
RESTMP = RESTMP / HUNDRED
UNTILHP = CHAR_MAX_HP * UNTILHPPERCENT
UNTILHP = UNTILHP / HUNDRED
UNTILMP = CHAR_MAX_MP * UNTILMPPERCENT
UNTILMP = UNTILMP / HUNDRED

////////MAIN SCRIPT//////////

//Creates a bounding box
IF BRDBOX != ZERO

    PRINT_TEXT "Creating Border"
    CLEAR_BORDER
    SLEEP 100
    
    BORDERX = CHAR_X + XRADIUS
    BORDERY = CHAR_Y + YRADIUS
    ADD_BORDER_PT "<&BORDERX&>" "<&BORDERY&>"

    BORDERX = CHAR_X + XRADIUS
    BORDERY = CHAR_Y - YRADIUS
    ADD_BORDER_PT "<&BORDERX&>" "<&BORDERY&>"

    BORDERX = CHAR_X - XRADIUS
    BORDERY = CHAR_Y - YRADIUS
    ADD_BORDER_PT "<&BORDERX&>" "<&BORDERY&>"

    BORDERX = CHAR_X - XRADIUS
    BORDERY = CHAR_Y + YRADIUS
    ADD_BORDER_PT "<&BORDERX&>" "<&BORDERY&>"
ENDIF


//just a printout 
PRINT_TEXT "rest below <&RESTHP&> HP, until <&UNTILHP&> HP"
PRINT_TEXT "rest below <&RESTMP&> MP, until <&UNTILMP&> MP"
IF DEBUG == ONE
    IF LOWHPSKILLID != ZERO
        PRINT_TEXT "using lowhp skill under <&LOWHP&> HP"
    ENDIF
    IF LOWMPSKILLID != ZERO
        PRINT_TEXT "using lowmp skill under <&LOWMP&> MP"
    ENDIF
    IF ATTACKSKILLID != ZERO
        PRINT_TEXT "using attack skill over <&SKILLMP&> MP"
    ENDIF
ENDIF

//Enables auto-ss
IF SSID != ZERO
    SWITCH SSID
        CASE #i1
            INJECT "D0 0D 00 2B 07 00 00 01 00 00 00"
            IF DEBUG == ONE
                PRINT_TEXT "Auto-using No Grade Soulshots"
            ENDIF
            BREAK 1
        CASE #i2
            INJECT "D0 0D 00 B7 05 00 00 01 00 00 00"
            IF DEBUG == ONE
                PRINT_TEXT "Auto-using D grade Soulshots"
            ENDIF
            BREAK 1
        CASE #i3
            INJECT "D0 0D 00 B8 05 00 00 01 00 00 00"
            IF DEBUG == ONE
                PRINT_TEXT "Autousing C grade Soulshots"
            ENDIF
            BREAK 1
        CASE #i4
            INJECT "D0 0D 00 B9 05 00 00 01 00 00 00"
            IF DEBUG == ONE
                PRINT_TEXT "Auto-using B grade Soulshots"
            ENDIF
            BREAK 1
        CASE #i5
            INJECT "D0 0D 00 BA 05 00 00 01 00 00 00"
            IF DEBUG == ONE
                PRINT_TEXT "Auto-using A grade SOulshots"
            ENDIF
            BREAK 1
        DEFAULT
            IF DEBUG == ONE
                PRINT_TEXT "Soulshot option incorectly setup"
            ENDIF
            BREAK 1
    ENDSWITCH
ENDIF    

//Enables auto-bsps
IF SPSID != ZERO
    SWITCH SPSID
        CASE #i1
            INJECT "D0 0D 00 6B 0F 00 00 01 00 00 00"
            IF DEBUG == ONE
                PRINT_TEXT "Auto-using No Grade SpiritShots"
            ENDIF
            BREAK 1
        CASE #i2
            INJECT "D0 0D 00 6C 0F 00 00 01 00 00 00"
            IF DEBUG == ONE
                PRINT_TEXT "Auto-using D grade SpiritShots"
            ENDIF
            BREAK 1
        CASE #i3
            INJECT "D0 0D 00 6D 0F 00 00 01 00 00 00"
            IF DEBUG == ONE
                PRINT_TEXT "Auto-using C grade SpiritShots"
            ENDIF
            BREAK 1
        CASE #i4
            INJECT "D0 0D 00 6E 0F 00 00 01 00 00 00"
            IF DEBUG == ONE
                PRINT_TEXT "Auto-using B grade SpiritShots"
            ENDIF
            BREAK 1
        CASE #i5
            INJECT "D0 0D 00 6F 0F 00 00 01 00 00 00"
            IF DEBUG == ONE
                PRINT_TEXT "Auto-using A grade SpiritShots"
            ENDIF
            BREAK 1
        DEFAULT
            IF DEBUG == ONE
                PRINT_TEXT "SpiritShots option incorectly setup"
            ENDIF
            BREAK 1
    ENDSWITCH
ENDIF
    


IF CHAR_CUR_HP != ZERO
    //don't rest if you are under attack
    IF COUNT_NPC_TARGETME == ZERO
        //lets make sure we start with decent hp
        IF CHAR_CUR_HP < RESTHP
            PRINT_TEXT "hp low, resting"
            CALLSUB Rest
        ENDIF
        //also make sure we start with decent mp
        IF CHAR_CUR_MP < RESTMP
            PRINT_TEXT "mp low, resting"
            CALLSUB Rest
        ENDIF
    ENDIF
ELSE
    CALLSUB Dead
ENDIF

IF DEBUG == ONE
    PRINT_TEXT "starting to lvl"
ENDIF

DO 
    DO 
    IF CHAR_CUR_HP != ZERO
        IF ISLEADER == ONE
            CALLSUB Target
        ELSE
            DO
                SLEEP 1000
                IF CHAR_CUR_HP == ZERO
                    CALLSUB Dead
                ENDIF
            LOOP TARGET_CUR_HP == ZERO
        ENDIF
        //if we got a target
        IF TARGET_CUR_HP > ZERO
            IF DEBUG == ONE
                PRINT_TEXT "got target"
            ENDIF
            //move near target first?
            IF MOVETO_TARGET == ONE
                CALLSUB MovetoTarget
            ENDIF
            //lets kill it
            CALLSUB KillTarget
        ENDIF

        //now our target is dead
        IF MANOR == ONE
            USE_ITEM "<&HARVID&>"
            PRINT_TEXT "Harvester used"
        ENDIF
        IF PICKUP == ONE
        GET_TIME StartTime
            IF DEBUG == ONE
                PRINT_TEXT "picking"
            ENDIF
            PICKUP_X = "#i<&CHAR_X&>"
            PICKUP_Y = "#i<&CHAR_Y&>"
            IF PICKFIRST == ZERO
                IF COUNT_NPC_TARGETME == ZERO
                    WHILE NEAREST_ITEM_DISTANCE < LOOT_RANGE
                        GET_TIME EndTime
                        UsedTime = EndTime - StartTime
                        IF DEBUG == ONE
                            PRINT_TEXT "Used Time: <&UsedTime&>"
                        ENDIF
                        IF CHAR_CUR_HP == ZERO
                            CALLSUB Dead
                        ENDIF
                        IF UsedTime > MaxPickTime
                            BREAK 5
                        ENDIF
                        DISTANCE TEMP CHAR_X CHAR_Y TEMP PICKUP_X PICKUP_Y TEMP
                        IF TEMP > LOOT_RANGE
                            IF DEBUG == ONE
                                PRINT_TEXT "outside lootrange, breaking pickup loop"
                            ENDIF
                            BREAK 5
                        ENDIF
                        CALLSUB Pickup
                    WEND
                ENDIF
            ELSE
                WHILE NEAREST_ITEM_DISTANCE < LOOT_RANGE
                    GET_TIME EndTime
                    UsedTime = EndTime - StartTime
                    IF DEBUG == ONE
                        PRINT_TEXT "Used Time: <&UsedTime&>"
                    ENDIF
                    IF CHAR_CUR_HP == ZERO
                        CALLSUB Dead
                    ENDIF
                    IF UsedTime > MaxPickTime
                        BREAK 4
                    ENDIF
                    DISTANCE TEMP CHAR_X CHAR_Y TEMP PICKUP_X PICKUP_Y TEMP
                    IF TEMP > LOOT_RANGE
                        IF DEBUG == ONE
                            PRINT_TEXT "outside lootrange, breaking pickup loop"
                        ENDIF
                        BREAK 4
                    ENDIF
                    CALLSUB Pickup
                WEND
            ENDIF
        ENDIF
        //mp regen skill?
        IF COUNT_NPC_TARGETME == ZERO
            IF LOWMPSKILLID != ZERO
                IF CHAR_CUR_MP < LOWMP
                    USE_SKILL "<&LOWMPSKILLID&>"
                    GET_RAND TEMP "<&MIN_LOWMPDELAY&>" "<&MAX_LOWMPDELAY&>"
                    SLEEP "<&TEMP&>"
                ENDIF
            ENDIF
        ENDIF
    ELSE
        CALLSUB Dead
    ENDIF
    //do we need to rest? if not lets keep fighting
    IF CHAR_CUR_HP != ZERO
        IF COUNT_NPC_TARGETME == ZERO
            IF CHAR_CUR_HP < RESTHP
                PRINT_TEXT "hp is below <&RESTHP&>, resting"
                CALLSUB Rest
            ENDIF
            IF CHAR_CUR_MP < RESTMP
                PRINT_TEXT "mp is below <&RESTMP&>, resting"
                CALLSUB Rest
            ENDIF
        ENDIF
    ENDIF
    LOOP CHAR_CUR_HP != ZERO
    
IF CHAR_CUR_HP == ZERO
    CALLSUB Dead
ENDIF
    

LOOP ZERO == ZERO

SCRIPT_END 

////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////SUBS//////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////

SUB Target
    IF DEBUG == ONE
        PRINT_TEXT "targeting"
    ENDIF
    TARGET_NEAREST
    SLEEP 200
    TARGET_NEAREST_NAME "Varka Silenos Great Sear"
    SLEEP 200
    TARGET_NEAREST_NAME "Varka Silenos Great Magus"
    GET_RAND TEMP "<&MIN_TARGETDELAY&>" "<&MAX_TARGETDELAY&>"
    SLEEP "<&TEMP&>"
RETURNSUB

SUB MovetoTarget
    DISTANCE DISTANCE_TO_TARGET CHAR_X CHAR_Y CHAR_Z TARGET_X TARGET_Y TARGET_Z    
    IF DISTANCE_TO_TARGET <= MOVETO_DISTANCE
        BREAK 9
    ENDIF
    IF DEBUG == ONE
        PRINT_TEXT "moving to target"
    ENDIF

    //IF RANGEDID != ZERO
        //MOVE_TO "<&TARGET_X&>" "<&TARGET_Y&>" "<&TARGET_Z&>"

        //CHECKS IF THE CHARACTER IS IN SKILL RANGE TO THE TARGET AND IF IT IS USES THE RANGED SKILL
        //DO
            //DISTANCE DISTANCE_TO_TARGET CHAR_X CHAR_Y CHAR_Z TARGET_X TARGET_Y TARGET_Z
            //IF DEBUG == ONE
                //PRINT_TEXT "Distance to skill range: <&DISTANCE_TO_TARGET&>"
            //ENDIF
            //SLEEP 100
        //LOOP DISTANCE_TO_TARGET > SKLRANGE
        //PRINT_TEXT "In skill range"
        //USE_SKILL "<&RANGEDID&>"
        //PRINT_TEXT "Using ranged skill"
        //GET_RAND TEMP "<&MIN_SKILLDELAY&>" "<&MAX_SKILLDELAY&>"
        //SLEEP "<&TEMP&>"
    //ENDIF
//Now we move to the target

   
DO
        MOVE_TO "<&TARGET_X&>" "<&TARGET_Y&>" "<&TARGET_Z&>"
        DISTANCE DISTANCE_TO_TARGET CHAR_X CHAR_Y CHAR_Z TARGET_X TARGET_Y TARGET_Z
        IF DEBUG == ONE
            PRINT_TEXT "Distance: <&DISTANCE_TO_TARGET&>"
        ENDIF
        SLEEP 100
    LOOP DISTANCE_TO_TARGET > MOVETO_DISTANCE
    IF DEBUG == ONE
        PRINT_TEXT "close enough"
    ENDIF
RETURNSUB



SUB Pickup
    CLICK_NEAREST_ITEM
    GET_RAND TEMP "<&MIN_PICKUPDELAY&>" "<&MAX_PICKUPDELAY&>"
    SLEEP "<&TEMP&>"
RETURNSUB


SUB KillTarget
    IF DEBUG == ONE
        PRINT_TEXT "killing target"
    ENDIF
    
    GET_TIME StartTime
    IF DEBUG == ONE
        PRINT_TEXT "Start time: <&StartTime&>"
    ENDIF
    
    DO
        GET_TIME EndTime
        UsedTime = EndTime - StartTime
        IF DEBUG == ONE
            PRINT_TEXT "Used Time: <&UsedTime&>"
        ENDIF        
        IF UsedTime > MaxTime
            IF TARGET_CUR_HP == TARGET_MAX_HP
                CALLSUB Stuck
                RETURNSUB
            ENDIF
        ENDIF

        IF ISSPOILER == ONE
            IF SPOILSKILLID != ZERO
                IF TARGET_SPOILED == ZERO
                    IF DEBUG == ONE
                        PRINT_TEXT "spoiling"
                    ENDIF
                    USE_SKILL "<&SPOILSKILLID&>"
                    GET_RAND TEMP "<&MIN_SPOILDELAY&>" "<&MAX_SPOILDELAY&>"
                    SLEEP "<&TEMP&>"
                ENDIF
            ENDIF
        ENDIF

        IF MANOR == ONE
            IF SEEDID != ZERO
                    IF DEBUG == ONE
                        PRINT_TExT "Seeding"
                    ENDIF
                    USE_ITEM "<&SEEDID&>"
                    GET_RAND TEMP "<&MIN_SPOILDELAY&>" "<&MAX_SPOILDELAY&>"
                    SLEEP "<&TEMP&>"
                    
            ENDIF
        ENDIF

        IF LOWHPSKILLID != ZERO
            IF CHAR_CUR_HP < LOWHP
                IF DEBUG == ONE
                    PRINT_TEXT "using lowhp skill"
                ENDIF
                USE_SKILL "<&LOWHPSKILLID&>"
                GET_RAND TEMP "<&MIN_LOWHPDELAY&>" "<&MAX_LOWHPDELAY&>"
                SLEEP "<&TEMP&>"
            ENDIF
        ENDIF
        IF ATTACKSKILLID != ZERO
            IF CHARTYPE == THREE
                IF TARGET_CUR_HP >= ATTACKHP
                    IF DEBUG == ONE
                        PRINT_TEXT "using attack skill"
                    ENDIF
                    IF SOULSHOTID != ZERO
                        USE_ITEM "<&SOULSHOTID&>"
                    ENDIF
                    USE_SKILL "<&ATTACKSKILLID&>"
                    SLEEP 300
                    USE_SKILL  "<&ATTACKSKILLID2&>"
                    GET_RAND TEMP "<&MIN_SKILLDELAY&>" "<&MAX_SKILLDELAY&>"
                    SLEEP "<&TEMP&>"
                ENDIF
            ELSE
                IF CHAR_CUR_MP > SKILLMP
                    IF DEBUG == ONE
                        PRINT_TEXT "using attack skill"
                    ENDIF
                    USE_SKILL "<&ATTACKSKILLID&>"
                    GET_RAND TEMP "<&MIN_SKILLDELAY&>" "<&MAX_SKILLDELAY&>"
                    SLEEP "<&TEMP&>"
                ENDIF
            ENDIF
        ENDIF

        IF CHARTYPE == THREE
            IF TARGET_CUR_HP < ATTACKHP
                IF TARGET_CUR_HP > ZERO
                    IF DEBUG == ONE
                        PRINT_TEXT "target low hp, going melee"
                    ENDIF
                    ATTACK_TARGET
                    GET_RAND TEMP "<&MIN_ATTACKDELAY&>" "<&MAX_ATTACKDELAY&>"
                    SLEEP "<&TEMP&>"
                ENDIF
            ENDIF        
        ELSE        
            IF TARGET_CUR_HP > ZERO
                IF DEBUG == ONE
                    PRINT_TEXT "attacking"
                ENDIF
                ATTACK_TARGET
                GET_RAND TEMP "<&MIN_ATTACKDELAY&>" "<&MAX_ATTACKDELAY&>"
                SLEEP "<&TEMP&>"
            ENDIF
        ENDIF
    IF CHAR_CUR_HP == ZERO
        CALLSUB Dead
    ENDIF
    
    LOOP TARGET_CUR_HP > ZERO

IF MANOR == one
    USE_ITEM "<&HARVID&>"
    PRINT_TEXT "Harvester used"
    GET_RAND TEMP "<&MIN_SWEEPDELAY&>" "<&MAX_SWEEPDELAY&>"
    SLEEP "<&TEMP&>"
ENDIF

IF ISSPOILER == ONE
    IF SWEEPSKILLID != ZERO
        IF TARGET_CUR_HP == ZERO
            IF DEBUG == ONE
                PRINT_TEXT "target spoiled, sweeping"
            ENDIF
            USE_SKILL "<&SWEEPSKILLID&>"
            GET_RAND TEMP "<&MIN_SWEEPDELAY&>" "<&MAX_SWEEPDELAY&>"
            SLEEP "<&TEMP&>"
        ENDIF
    ENDIF
ENDIF

RETURNSUB


SUB Rest
    PRINT_TEXT "starting to rest until <&UNTILHP&> HP and <&UNTILMP&> MP"

    Rest = ONE
    SLEEP 1000
    IF DEBUG == ONE
        PRINT_TEXT "sitting down"
    ENDIF
    COMMAND "/sit"
    DO
        //check if anything has us targeted
        IF COUNT_NPC_TARGETME != ZERO
            //we are targeted, let not be sitting shall we?
            Rest = #i0
            IF DEBUG == ONE
                PRINT_TEXT "Some monster attacked us, lets defend ourselves"
            ENDIF
        ENDIF

        //enough hp?
        IF CHAR_CUR_HP >= UNTILHP
            //enough mp?
            IF CHAR_CUR_MP >= UNTILMP
                Rest = #i0
                PRINT_TEXT "We are ready to go."
            ENDIF
        ENDIF

    //are we still resting? if so lets run the loop again
    SLEEP 1000
    LOOP Rest == ONE

    //done resting lets stand
    IF DEBUG == ONE
        PRINT_TEXT "standing up"
    ENDIF
    COMMAND "/stand"
    SLEEP 200
RETURNSUB

SUB Dead
    IF LOGOUTTIME != ZERO
        PRINT_TEXT "Your Dead, Dumbass. Waiting <&LOGOUTTIME&> Seconds for Ress"
    ELSE
        PRINT_TEXT "Your Dead, Dumbass. Waiting for Ress"
    ENDIF
    DEATHCOUNT = DEATHCOUNT + ONE
    PRINT_TEXT "you died <&DEATHCOUNT&> times until now"
    
    IF MAXDEATHS != ZERO
        IF DEATHCOUNT >= MAXDEATHS
            PRINT_TEXT "died too often, suspicous. logout for now, stopping script"
            //waiting 15 seconds before logout, because else might still be in combat
            SLEEP 15000
            FORCE_LOG
            END_SCRIPT
        ENDIF
    ENDIF
        
    DO
        SLEEP 1000
    IF LOGOUTTIME != ZERO
        COUNT = COUNT + ONE
        IF DEBUG == ONE
            PRINT_TEXT "<&COUNT&> seconds passed"
        ENDIF
        IF COUNT >= LOGOUTTIME
            PRINT_TEXT "Dead for too long, logging out, stopping script"
            //waiting 15 seconds before logout, because else might still be in combat
            SLEEP 15000
            INJECT "09"
            END_SCRIPT
        ENDIF
    ENDIF
    LOOP CHAR_CUR_HP == ZERO
    COUNT = #i0
    PRINT_TEXT "Looks like your alive again, lucky bastard"
    PRINT_TEXT "continue with script"
RETURNSUB

SUB Stuck
    IF DEBUG == ONE
        PRINT_TEXT "Looks like we are stuck, moving character"
    ENDIF
    X = CHAR_X - Range
    Y = CHAR_Y - Range
    MOVE_TO "<&X&>" "<&Y&>"  "<&CHAR_Z&>"
    SLEEP 1000
    X = CHAR_X + Range
    MOVE_TO "<&X&>" "<&CHAR_Y&>"  "<&CHAR_Z&>"
RETURNSUB 
////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////THE END ^_^/////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////