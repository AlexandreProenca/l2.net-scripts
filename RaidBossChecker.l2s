// RaidBoss Checker
// Server: all
// Autor: mochitto
// Date: 6. 6. 2010 Last Edited: 22. 11. 2010
// Special thanks to L2.NET contributors
// Version: L2.NET 378
// rev.: 28
PRINT_TEXT "SCRIPT STARTED!]"
INCLUDE "StdLib/NPC.l2c"

// Define section
// ===============================
DEFINE_GLOBAL STRING RB_NAME   "Flame of Splendor Barakiel"   // RaidBoss full name. You can search on http://l2jdp.com/l2jdb
DEFINE_GLOBAL STRING MSG_ALIVE "<&RB_NAME&> is alive!!"       // Custom message for RaidBoss spawn You can use <&RB_NAME&>
DEFINE_GLOBAL INT    CHECK_INTERVAL 5000                      // Checking interval in ms (1sec = 1000ms)
DEFINE_GLOBAL INT    ALARM        1                           // if 1 - On RB spawn play l2.net alarm and show message box 
DEFINE_GLOBAL INT    COUNTPMS     1                           // count pm then be sended
DEFINE_GLOBAL INT    PMSINTERVAL  10000                       // inteval with pm pack send in ms (1s = 1000ms)

// == DONT EDIT
DEFINE_GLOBAL ARRAYLIST PLAYERS 0
DEFINE_GLOBAL ARRAYLIST NPCS    0     
DEFINE_GLOBAL INT       CAN_CHECK  1     
// ==

// Players NAME list who will send message when RB will be alive
// You can add next playrs if put next line: PLAYERS.ADD "#$playerNick"
PLAYERS.ADD "#$Player1"
PLAYERS.ADD "#$Player2"

  
// Main section - DONT EDIT NEXT
// ===============================  
WHILE CAN_CHECK == #i1
     CHECK_RB
     SLEEP "<&CHECK_INTERVAL&>"
WEND

PRINT_TEXT "STOP SCRIPT!]"
END_SCRIPT

// Function section
// ===============================
FUNCTION CHECK_RB
     GET_NPCS NPCS
     FOREACH A NPC NPCS 
          IF ( ( NPCS.A.NAME.TO_UPPER == RB_NAME.TO_UPPER ) && ( CAN_CHECK == #i1 ) )
               PRINT_TEXT  "== <&RB_NAME&> ALIVE <&CAN_CHECK&>"
               CAN_CHECK = #i0
               IF ALARM == #i1
                    NMESSAGE_BOX "RaidBoss ALIVE" "RaidBoss: <&RB_NAME&> is alive!" "<&INFORMATION&>"
                    PLAYALARM
               ENDIF     
               ALERT_PLAYERS     
          ENDIF
     NEXTEACH
     IF CAN_CHECK == #i1
          PRINT_TEXT "== <&RB_NAME&> DEAD"
     ENDIF
RETURN VOID 

FUNCTION ALERT_PLAYERS
     FOR B 0 "<&COUNTPMS&>" 1
          FOREACH A STRING PLAYERS
               SAY_TEXT "<&CHANNEL_PRIVATE&>" "<&MSG_ALIVE&>" "<&PLAYERS.A&>"
               SLEEP 1000
          NEXTEACH
          SLEEP "<&PMSINTERVAL&>"
     NEXT
     PRINT_TEXT "== MESSAGES SENDED"
RETURN VOID