// Edited iosc AIO enchant script
// Server: L2 S G H.com
// Author: iosc
// Date: 18. 6. 2010
// Special thanks for L2.NET contributors and slothmo, mpj123, rocket666, easy2k4, trixy
// rev.: 21
PRINT_TEXT "SCRIPT STARTED!]"
INCLUDE StdLib\inventory.l2c

// Event section
// ===============================
SET_EVENT "<&SCRIPTEVENT_SERVERPACKET&>" "<&SYSTEM_CURRENTFILE&>" AskJoinPartyPacket 57
SET_EVENT "<&SCRIPTEVENT_SERVERPACKET&>" "<&SYSTEM_CURRENTFILE&>" TradeRequestPacket 112
SET_EVENT "<&SCRIPTEVENT_SERVERPACKET&>" "<&SYSTEM_CURRENTFILE&>" AskJoinPledgePacket 44
SET_EVENT "<&SCRIPTEVENT_SYSTEMMESSAGE&>" "<&SYSTEM_CURRENTFILE&>" SystemMessages

// Define section
// ===============================
DEFINE_GLOBAL INT buy_scrolls 1        // Buy new scrolls?
DEFINE_GLOBAL INT enchant_to_top 4     // Enchant to +?

// PLEASE DONT EDIT NEXT!!
DEFINE_GLOBAL STRING item_name "Staff of Master Yogi "
DEFINE_GLOBAL STRING normal_enchant_name "Master Yogi's Scroll: Enchant Weapon"
                                              
DEFINE_GLOBAL INT min_little_delay 320
DEFINE_GLOBAL INT max_little_delay 407

DEFINE_GLOBAL INT min_delay 2118
DEFINE_GLOBAL INT max_delay 2333
DEFINE_GLOBAL INT rand_delay 0
DEFINE_GLOBAL INT some_loop 0
DEFINE_GLOBAL INT item_uid 0
DEFINE_GLOBAL INT enchant_result 9999
DEFINE_GLOBAL INT n_scroll_id 13540
DEFINE_GLOBAL INT n_scrolls_count 0
DEFINE_GLOBAL Inventory n_enchant_scroll 0
DEFINE_GLOBAL ARRAYLIST full_inventory 0
DEFINE_GLOBAL ARRAYLIST remove_uid 0

// Main section
// ===============================
GET_INVENTORY full_inventory
PRINT_TEXT "3 seconds ========================"
PRINT_TEXT "== Temp Auto Enchant Script"
PRINT_TEXT "================================"
COMMAND "/target Master Yogi"
SLEEP 1000
PRINT_TEXT "== "
PRINT_TEXT "== Enchanting: <&item_name&>"
PRINT_TEXT "== Enchant till: +<&enchant_to_top&>"
PRINT_TEXT "== "
PRINT_TEXT "2 seconds ========================="
SLEEP 2000

ITEM_COUNT n_scrolls_count "<&n_scroll_id&>"

FOREACH x Inventory full_inventory
	IF full_inventory.x.name == normal_enchant_name
		n_enchant_scroll = full_inventory.x
	ENDIF
NEXTEACH

IF n_scrolls_count < #i2
	PRINT_TEXT "You don't have <&normal_enchant_name&>"
	IF buy_scrolls == #i1
		TALK_TARGET
		SLEEP 2000
		NPC_DIALOG "bypass -h Quest MasterOfEnchanting 32599-participate.htm"
		SLEEP 2000
		NPC_DIALOG "bypass -h Quest MasterOfEnchanting buy_scroll_10"
		SLEEP 2000
	ELSE
		JUMP_TO_LABEL THE_END
	ENDIF
ENDIF

CALL EnchantEachOnetoTop 

LABEL THE_END
GET_RAND rand_delay "<&min_little_delay&>" "<&max_little_delay&>"
SLEEP "<&rand_delay&>"
full_inventory.clear
GET_INVENTORY full_inventory
FOREACH x Inventory full_inventory
	IF full_inventory.x.name == item_name
		PRINT_TEXT "==  <&item_name&> +<&full_inventory.x.enchant&>"
	ENDIF 
NEXTEACH
PRINT_TEXT "=======================iosc======"

END_SCRIPT

FUNCTION EnchantEachOnetoTop
	DEFINE INT enchant_level 0
	DEFINE INT lag_flag3 0
	DEFINE INT lag_flag4 0
	DEFINE INT lag_flag5 0
	DEFINE INT adena_count 0
	LABEL B_FLAGS

	full_inventory.clear
	GET_INVENTORY full_inventory
	item_uid = ZERO
	enchant_level = ZERO
	ITEM_COUNT n_scrolls_count "<&n_scroll_id&>"
	ITEM_COUNT adena_count 57

	FOREACH x Inventory full_inventory
		IF full_inventory.x.name == item_name
			IF full_inventory.x.enchant < enchant_to_top
				item_uid = full_inventory.x.id
				enchant_level = full_inventory.x.enchant
				PRINT_TEXT "HAVE STAFF FOR ENCHANT!"
			ELSE    	
				PRINT_TEXT "Have +<&full_inventory.x.enchant&> <&full_inventory.x.name&> get for reward..."
				SLEEP 1000
				USE_ITEM "<&full_inventory.x.ITEM_ID&>"
				SLEEP 1000
				TALK_TARGET
				SLEEP 2000
				NPC_DIALOG "bypass -h Quest MasterOfEnchanting receive_reward"
				SLEEP 2000
				//SendStatistics
				JUMP_TO_LABEL B_FLAGS
			ENDIF
		ENDIF 
	NEXTEACH    
    
	IF item_uid == ZERO
		lag_flag3 = lag_flag3 + ONE
		IF lag_flag3 == #i3
			PRINT_TEXT "Buy new staff"
			TALK_TARGET
			SLEEP 2000
			NPC_DIALOG "bypass -h Quest MasterOfEnchanting 32599-participate.htm"
			SLEEP 2000
			NPC_DIALOG "bypass -h Quest MasterOfEnchanting buy_staff"
			SLEEP 2000
		ENDIF
		JUMP_TO_LABEL B_FLAGS
	ENDIF
    
	IF adena_count < 1000000
		PRINT_TEXT "You dont have adena..."
		JUMP_TO_LABEL THE_END
	ENDIF
    
	IF n_scrolls_count == ONE
		lag_flag4 = lag_flag4 + ONE
		IF lag_flag4 == #i3
			PRINT_TEXT "You are out of <&normal_enchant_name&>"
			IF buy_scrolls == #i1
				TALK_TARGET
				SLEEP 2000
				NPC_DIALOG "bypass -h Quest MasterOfEnchanting 32599-participate.htm"
				SLEEP 2000
				NPC_DIALOG "bypass -h Quest MasterOfEnchanting buy_scroll_10"
				SLEEP 2000
			ELSE
  				JUMP_TO_LABEL THE_END
			ENDIF
		ENDIF
		JUMP_TO_LABEL B_FLAGS
	ENDIF
	lag_flag3 = ZERO
	lag_flag4 = ZERO     
    
	DO
		PRINT_TEXT "== ENCHANT PROCESS"
		IF n_scrolls_count == ONE
			PRINT_TEXT "You are out of <&normal_enchant_name&>"
			IF buy_scrolls == #i1
				TALK_TARGET
				SLEEP 2000
				NPC_DIALOG "bypass -h Quest MasterOfEnchanting 32599-participate.htm"
				SLEEP 2000
				NPC_DIALOG "bypass -h Quest MasterOfEnchanting buy_scroll_10"
				SLEEP 2000
			ELSE
				JUMP_TO_LABEL THE_END
			ENDIF
		ENDIF
    
		CALL Enchanting VOID 1 n_enchant_scroll.id
		ITEM_COUNT n_scrolls_count "<&n_scroll_id&>"
            
		IF enchant_result > ZERO
			PRINT_TEXT "<&item_name&> was cristalized at +<&enchant_level&>. (<&n_scrolls_count&> scrolls left)"
			remove_uid.ADD "#i<&item_uid&>"
			JUMP_TO_LABEL B_FLAGS
		ELSE
			enchant_level = enchant_level + ONE
			PRINT_TEXT "Enchanting <&item_name&> succeeded to +<&enchant_level&>. (<&n_scrolls_count&> scrolls left)"
		ENDIF      
	LOOP enchant_level < enchant_to_top  
	    
	JUMP_TO_LABEL B_FLAGS  
RETURN VOID

FUNCTION Enchanting 1 scroll_id 
	enchant_result = #i9999
	some_loop = ZERO
	GET_RAND rand_delay "<&min_little_delay&>" "<&max_little_delay&>"
	SLEEP "<&rand_delay&>"
	SET_EVENT "<&SCRIPTEVENT_SERVERPACKET&>" "<&SYSTEM_CURRENTFILE&>" InsertItem 124
	DEFINE BYTEBUFFER use_scroll_buffer 256
	use_scroll_buffer.write_byte #i25
	use_scroll_buffer.write_int32 scroll_id
	use_scroll_buffer.write_int32 #i0
	use_scroll_buffer.trim_to_index
	INJECTBB use_scroll_buffer
	DELETE use_scroll_buffer
    
	WHILE some_loop == ZERO
		SLEEP 50
	WEND
	LOCK A
	SLEEP 50
	UNLOCK A        
RETURN VOID 

FUNCTION InsertItem      
	LOCK A
	some_loop = ONE
    
	SET_EVENT "<&SCRIPTEVENT_SERVERPACKET&>" "<&SYSTEM_CURRENTFILE&>" BlankFunc 124
	SET_EVENT "<&SCRIPTEVENT_SERVERPACKETEX&>" "<&SYSTEM_CURRENTFILE&>" ClickOK 129
    
	GET_RAND rand_delay "<&min_little_delay&>" "<&max_little_delay&>"
	SLEEP "<&rand_delay&>"
    
	DEFINE BYTEBUFFER use_item_buffer 256
	use_item_buffer.write_byte #i208
	use_item_buffer.write_int16 #i76
	use_item_buffer.write_int32 item_uid
	use_item_buffer.trim_to_index
	INJECTBB use_item_buffer
	DELETE use_item_buffer        
RETURN VOID    

FUNCTION ClickOK   
	SET_EVENT "<&SCRIPTEVENT_SERVERPACKETEX&>" "<&SYSTEM_CURRENTFILE&>" BlankFunc 129
	SET_EVENT "<&SCRIPTEVENT_SERVERPACKET&>" "<&SYSTEM_CURRENTFILE&>" EnchantComplete 135

	GET_RAND rand_delay "<&min_delay&>" "<&max_delay&>"
	SLEEP "<&rand_delay&>"
    
	DEFINE BYTEBUFFER confirm_enchant_buffer 256
	confirm_enchant_buffer.write_byte #i95
	confirm_enchant_buffer.write_int32 item_uid
	confirm_enchant_buffer.write_int32 #i0
	confirm_enchant_buffer.trim_to_index
	INJECTBB confirm_enchant_buffer
	DELETE confirm_enchant_buffer   
RETURN VOID   

FUNCTION EnchantComplete 
	SET_EVENT "<&SCRIPTEVENT_SERVERPACKET&>" "<&SYSTEM_CURRENTFILE&>" BlankFunc 135      
	DEFINE INT result_byte 0     
	PACKET.READ_BYTE result_byte
	PACKET.READ_INT32 enchant_result      
	DELETE result_byte       
	UNLOCK A      
RETURN VOID
                
FUNCTION BlankFunc
	SLEEP 25 //e = mc^2 nonsence
RETURN VOID

FUNCTION AskJoinPartyPacket
	DEFINE INT RAND 0
	GET_RAND RAND 1000 2000
	SLEEP "<&RAND&>"
	INJECT "43 00 00 00 00" // reject party
RETURN VOID

FUNCTION TradeRequestPacket
	DEFINE INT RAND 0
	GET_RAND RAND 1000 2000
	SLEEP "<&RAND&>"
	INJECT "55 00 00 00 00" // reject trade
RETURN VOID

FUNCTION AskJoinPledgePacket
	DEFINE INT RAND 0
	GET_RAND RAND 1000 2000
	SLEEP "<&RAND&>"
	INJECT "27 00 00 00 00" // reject clan
RETURN VOID 

FUNCTION SendStatistics
	DEFINE INT rewardCount 0
	ITEM_COUNT rewardCount 6577//  6578 as
	TEST_WEBSITE STATUS "http://l2net.mochitto.com/yogi.php?charname=<&CHAR_NAME&>&totalcount=<&rewardCount&>"
RETURN VOID