// AEGIS/functions/CounterDisarm.l2s
// Intelligently Manages weapon disarm

FUNCTION CounterDisarmAdmin

  IF "AEGIS.OPTIONS.CONFIG.CONTAINS_KEY COUNTER_DISARM" == FALSE
    AEGIS.OPTIONS.CONFIG.ADD FALSE "COUNTER_DISARM"
  ENDIF

  IF "AEGIS.OPTIONS.TEMP.CONTAINS_KEY CounterDisarm" == FALSE
    LOCK AEGIS.OPTIONS.TEMP.MUTEX
      DEFINE INT WEAPON 0
      AEGIS.OPTIONS.TEMP.ADD WEAPON "CounterDisarm"
    UNLOCK AEGIS.OPTIONS.TEMP.MUTEX
  ENDIF

  IF "AEGIS.OPTIONS.CONFIG.#$COUNTER_DISARM" == TRUE
    PRINT_TEXT "Counter Disarm enabled."
    THREAD CounterDisarm
  ELSE
    PRINT_TEXT "Counter Disarm disabled."
  ENDIF
  
RETURN VOID

FUNCTION CounterDisarm
  
  DEFINE INT DISARMED 0
  DEFINE INT DISABLED 0
  
  WHILE "AEGIS.OPTIONS.CONFIG.#$COUNTER_DISARM" == TRUE
    IF (("AEGIS.EFFECTTRACKING.ABNORMALEFFECTS.CONTAINS_KEY 485" == TRUE) || ("AEGIS.EFFECTTRACKING.ABNORMALEFFECTS.CONTAINS_KEY 775" == TRUE)) || ("AEGIS.EFFECTTRACKING.ABNORMALEFFECTS.CONTAINS_KEY 794" == TRUE)
     
      DEFINE BYTEBUFFER _bUseItem 4096
     
      IF "AEGIS.OPTIONS.TEMP.#$CounterDisarm" != ZERO
        _bUseItem.WRITE_BYTE #I25
        _bUseItem.WRITE_INT32 "AEGIS.OPTIONS.TEMP.#$CounterDisarm"
        _bUseItem.WRITE_INT32 ZERO
        _bUseItem.TRIM_TO_INDEX
        _buseItem.RESET_INDEX
        
        WHILE (("AEGIS.EFFECTTRACKING.ABNORMALEFFECTS.CONTAINS_KEY 485" == TRUE) || ("AEGIS.EFFECTTRACKING.ABNORMALEFFECTS.CONTAINS_KEY 775" == TRUE)) || ("AEGIS.EFFECTTRACKING.ABNORMALEFFECTS.CONTAINS_KEY 794" == TRUE)
          SLEEP 250
        WEND
      
        StatusCheck DISABLED 0
      
        WHILE DISABLED == TRUE
          StatusCheck DISABLED 0
          SLEEP 500
        WEND
      
        DISARMED = AEGIS.ENTITYTRACKING.CHARACTER.USERINFO.#$LRHAND_SLOT_OID + AEGIS.ENTITYTRACKING.CHARACTER.USERINFO.#$R_HAND_SLOT_OID
        WHILE DISARMED == ZERO          
          INJECTBB _bUseItem
          SLEEP 1000
          DISARMED = AEGIS.ENTITYTRACKING.CHARACTER.USERINFO.#$LRHAND_SLOT_OID + AEGIS.ENTITYTRACKING.CHARACTER.USERINFO.#$R_HAND_SLOT_OID
        WEND
        
      ENDIF
      DELETE _bUseItem  
    ENDIF
    DISARMED = FALSE
    SLEEP 250
  WEND

RETURN VOID

FUNCTION AEGIS::CounterDisarm::ProcessMSU 1 _oMagicSkillUse

  DEFINE INT DIST 0
  
  SWITCH _oMagicSkillUse.#$SKILL
    CASE #I485
    CASE #I775
      IF "_oMagicSkillUse.#$Target" == CHAR_ID
        IF AEGIS.ENTITYTRACKING.CHARACTER.USERINFO.#$R_HAND_SLOT_OID != ZERO
          "AEGIS.OPTIONS.TEMP.#$CounterDisarm" = AEGIS.ENTITYTRACKING.CHARACTER.USERINFO.#$R_HAND_SLOT_OID
        ELSE
          "AEGIS.OPTIONS.TEMP.#$CounterDisarm" = AEGIS.ENTITYTRACKING.CHARACTER.USERINFO.#$LRHAND_SLOT_OID
        ENDIF
      ENDIF
      BREAK 1
    CASE #I794
      DISTANCE DIST CHAR_X CHAR_Y CHAR_Z _oMagicSkillUse.#$Source_X _oMagicSkillUse.#$Source_Y _oMagicSkillUse.#$Source_Z
      IF DIST <= #I200
        IF AEGIS.ENTITYTRACKING.CHARACTER.USERINFO.#$R_HAND_SLOT_OID != ZERO
          "AEGIS.OPTIONS.TEMP.#$CounterDisarm" = AEGIS.ENTITYTRACKING.CHARACTER.USERINFO.#$R_HAND_SLOT_OID
        ELSE
          "AEGIS.OPTIONS.TEMP.#$CounterDisarm" = AEGIS.ENTITYTRACKING.CHARACTER.USERINFO.#$LRHAND_SLOT_OID
        ENDIF
      ENDIF
      BREAK 1
  ENDSWITCH

RETURN VOID

FUNCTION StatusCheck

  DEFINE INT B_RESULT
  DEFINE INT DISABLED 0
  
  // Invulnerable
  B_RESULT = "AEGIS.ENTITYTRACKING.CHARACTER.USERINFO.#$EXTENDED_VFX" & #I1
  IF B_RESULT == #I1
    DISABLED = TRUE
  ENDIF
  // Air Stun
  B_RESULT = "AEGIS.ENTITYTRACKING.CHARACTER.USERINFO.#$EXTENDED_VFX" & #I2
  IF B_RESULT == #I2
    DISABLED = TRUE
  ENDIF
  // Afraid
  B_RESULT = "AEGIS.ENTITYTRACKING.CHARACTER.USERINFO.#$ABNORMAL_VFX" & #I32
  IF B_RESULT == #I32
    DISABLED = TRUE
  ENDIF
  // Stunned
  B_RESULT = "AEGIS.ENTITYTRACKING.CHARACTER.USERINFO.#$ABNORMAL_VFX" & #I64
  IF B_RESULT == #I64
    DISABLED = TRUE
  ENDIF
  // Asleep
  B_RESULT = "AEGIS.ENTITYTRACKING.CHARACTER.USERINFO.#$ABNORMAL_VFX" & #I128
  IF B_RESULT == #I128
    DISABLED = TRUE
  ENDIF
  // Paralyzed
  B_RESULT = "AEGIS.ENTITYTRACKING.CHARACTER.USERINFO.#$ABNORMAL_VFX" & #I1024
  IF B_RESULT == #I1024
    DISABLED = TRUE
  ENDIF
  // Petrified
  B_RESULT = "AEGIS.ENTITYTRACKING.CHARACTER.USERINFO.#$ABNORMAL_VFX" & #I2048
  IF B_RESULT == #I2048
    DISABLED = TRUE
  ENDIF


RETURN DISABLED 
