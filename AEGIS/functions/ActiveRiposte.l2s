// AEGIS/functions/ActiveRiposte.l2s
// When debuff is used on you, try and riposte before it is finished casting.

FUNCTION ActiveRiposteAdmin

  IF "AEGIS.OPTIONS.CONFIG.CONTAINS_KEY ACTIVE_RIPOSTE" == FALSE
    AEGIS.OPTIONS.CONFIG.ADD FALSE "ACTIVE_RIPOSTE"
  ENDIF

  IF "AEGIS.OPTIONS.CONFIG.#$ACTIVE_RIPOSTE" == TRUE
  
    IF "AEGIS.OPTIONS.CONFIG.CONTAINS_KEY RIPOSTE_MP_THRESHOLD" == FALSE
      PRINT_TEXT "ACTIVE_RIPOSTE set to enabled but RIPOSTE_MP_THRESHOLD is undefined."
      "AEGIS.OPTIONS.CONFIG.#$ACTIVE_RIPOSTE" = FALSE
    ENDIF
    IF "AEGIS.OPTIONS.CONFIG.CONTAINS_KEY RIPOSTE_SKILLS" == FALSE
      PRINT_TEXT "ACTIVE_RIPOSTE set to enabled but RIPOSTE_SKILLS is undefined."
      "AEGIS.OPTIONS.CONFIG.#$ACTIVE_RIPOSTE" = FALSE
    ENDIF
    
    IF "AEGIS.OPTIONS.CONFIG.CONTAINS_KEY RIPOSTE_SKILLS" == TRUE
      IF "AEGIS.OPTIONS.CONFIG.#$RIPOSTE_SKILLS.CLASSNAME" == "#$STRING"
        DEFINE STRING RIPOSTE_TMP "<&AEGIS.OPTIONS.CONFIG.#$RIPOSTE_SKILLS&>"
        AEGIS.OPTIONS.CONFIG.REMOVE "RIPOSTE_SKILLS"
        DEFINE SORTEDLIST LIST
        LIST.ADD "#$<&RIPOSTE_TMP&>" "<&RIPOSTE_TMP&>"
        AEGIS.OPTIONS.CONFIG.ADD LIST "RIPOSTE_SKILLS"
      ENDIF
    ENDIF
    
    IF "AEGIS.OPTIONS.CONFIG.CONTAINS_KEY RIPOSTE_TIME" == FALSE
      AEGIS.OPTIONS.CONFIG.ADD #I3000 "RIPOSTE_TIME"
    ENDIF
  ENDIF

  IF "AEGIS.OPTIONS.TEMP.CONTAINS_KEY StopRiposte" == FALSE
    LOCK AEGIS.OPTIONS.TEMP.MUTEX
      AEGIS.OPTIONS.TEMP.ADD ZERO "StopRiposte"
    UNLOCK AEGIS.OPTIONS.TEMP.MUTEX
  ENDIF
  
  IF "AEGIS.OPTIONS.CONFIG.#$ACTIVE_RIPOSTE" == TRUE
    PRINT_TEXT "Active Riposting enabled."
  ELSE
    PRINT_TEXT "Active Riposting disabled."
  ENDIF
  
RETURN VOID

FUNCTION AEGIS::ActiveRiposte::ProcessMSU 1 _oMagicSkillUse

  DEFINE INT DISABLED 0
  DEFINE INT RIPOSTE_STANCE 340
  DEFINE INT DELAY
  DEFINE DOUBLE MP_PERCENT 0.0
  DEFINE INT DIST

  IF "AEGIS.OPTIONS.CONFIG.#$RIPOSTE_AE_SKILLS.CONTAINS_KEY <&_oMagicSkillUse.#$SKILL&>" == TRUE 
    DISTANCE DIST CHAR_X CHAR_Y CHAR_Z _oMagicSkillUse.#$Source_X _oMagicSkillUse.#$Source_Y _oMagicSkillUse.#$Source_Z
    IF DIST <= "AEGIS.OPTIONS.CONFIG.#$RIPOSTE_AE_RANGE"
      StatusCheck DISABLED 0
      IF DISABLED == FALSE           
        THREAD CycleRiposte
        JUMP_TO_LABEL AEGIS::ActiveRiposte::ProcessMSU::Exit
      ENDIF
    ENDIF
  ENDIF
  
  IF "_oMagicSkillUse.#$Target" == CHAR_ID
    IF "AEGIS.OPTIONS.CONFIG.#$RIPOSTE_SKILLS.CONTAINS_KEY <&_oMagicSkillUse.#$SKILL&>" == TRUE
      StatusCheck DISABLED 0
      IF DISABLED == FALSE
        THREAD CycleRiposte
      ENDIF
    ENDIF
  ENDIF  

  LABEL AEGIS::ActiveRiposte::ProcessMSU::Exit
  
RETURN VOID

FUNCTION CycleRiposte
  
  DEFINE INT RIPOSTE_STANCE 340
  DEFINE INT DISABLED 0
  DEFINE DOUBLE MP_PERCENT 0.0
  
  IF "AEGIS.EFFECTTRACKING.ABNORMALEFFECTS.CONTAINS_KEY <&RIPOSTE_STANCE&>" == FALSE
    AEGIS.OPTIONS.TEMP.#$StopRiposte = TRUE
    MP_PERCENT = CHAR_CUR_MP / CHAR_MAX_MP
    MP_PERCENT = MP_PERCENT * #D100.0
    IF MP_PERCENT >= "AEGIS.OPTIONS.CONFIG.#$RIPOSTE_MP_THRESHOLD" 
      USE_SKILL "<&RIPOSTE_STANCE&>"
      SLEEP "<&AEGIS.OPTIONS.CONFIG.#$RIPOSTE_TIME&>"
    ENDIF
    AEGIS.OPTIONS.TEMP.#$StopRiposte = FALSE
  ENDIF

  IF AEGIS.OPTIONS.TEMP.#$StopRiposte == FALSE
    AEGIS.OPTIONS.TEMP.#$StopRiposte = TRUE
    
    IF "AEGIS.EFFECTTRACKING.ABNORMALEFFECTS.CONTAINS_KEY <&RIPOSTE_STANCE&>" == TRUE
      PRINT_TEXT "Trying to disable riposte stance"
      StatusCheck DISABLED 0
      
      WHILE DISABLED == TRUE
        StatusCheck DISABLED 0
        SLEEP 200
      WEND
      
      USE_SKILL "<&RIPOSTE_STANCE&>"
      SLEEP 500
    ENDIF
    
    AEGIS.OPTIONS.TEMP.#$StopRiposte = FALSE
    
  ENDIF
  
RETURN VOID

FUNCTION StatusCheck

  DEFINE INT B_RESULT
  DEFINE INT DISABLED 0
  
  // Invulnerable
  B_RESULT = "AEGIS.ENTITYTRACKING.CHARACTER.UserInfo.#$EXTENDED_VFX" & #I1
  IF B_RESULT == #I1
    DISABLED = TRUE
  ENDIF
  // Air Stun
  B_RESULT = "AEGIS.ENTITYTRACKING.CHARACTER.UserInfo.#$EXTENDED_VFX" & #I2
  IF B_RESULT == #I2
    DISABLED = TRUE
  ENDIF
  // Afraid
  B_RESULT = "AEGIS.ENTITYTRACKING.CHARACTER.UserInfo.#$ABNORMAL_VFX" & #I32
  IF B_RESULT == #I32
    DISABLED = TRUE
  ENDIF
  // Stunned
  B_RESULT = "AEGIS.ENTITYTRACKING.CHARACTER.UserInfo.#$ABNORMAL_VFX" & #I64
  IF B_RESULT == #I64
    DISABLED = TRUE
  ENDIF
  // Asleep
  B_RESULT = "AEGIS.ENTITYTRACKING.CHARACTER.UserInfo.#$ABNORMAL_VFX" & #I128
  IF B_RESULT == #I128
    DISABLED = TRUE
  ENDIF
  // Paralyzed
  B_RESULT = "AEGIS.ENTITYTRACKING.CHARACTER.UserInfo.#$ABNORMAL_VFX" & #I1024
  IF B_RESULT == #I1024
    DISABLED = TRUE
  ENDIF
  // Petrified
  B_RESULT = "AEGIS.ENTITYTRACKING.CHARACTER.UserInfo.#$ABNORMAL_VFX" & #I2048
  IF B_RESULT == #I2048
    DISABLED = TRUE
  ENDIF
  // Lesser Celestial Shield
  IF "AEGIS.EFFECTTRACKING.ABNORMALEFFECTS.CONTAINS_KEY 3158" == TRUE
    DISABLED = TRUE
  ENDIF

RETURN DISABLED